apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    zip64 = true
    dependencies {
//        exclude("$rootDir/ai/src")
//        exclude(project(':common'))
    }
}

mainClassName = 'no.habitats.corpus.spark.SparkUtil'

dependencies {
    compile project(':common')
    compile 'javax.mail:javax.mail-api:1.5.5'

    // DL4J histogram
    compile('org.glassfish.jersey.core:jersey-server:2.22.2') { force = true }
    compile('org.glassfish.jersey.containers:jersey-container-servlet-core:2.22.2') { force = true }
    compile('org.glassfish.jersey.containers:jersey-container-servlet:2.22.2') { force = true }
}

task pathingJar(type: Jar) {
    dependsOn configurations.runtime
    appendix = 'pathing'

    doFirst {
        manifest {
            // Build the Class-Path for absolute paths based on runtime dependencies.
            attributes "Class-Path": configurations.runtime.files.collect {
                it.toURL().toString().replaceFirst(/file:\/+/, '/')
            }.join(' ')
        }
    }
}

task corpus(type: JavaExec) {
    dependsOn compileScala, pathingJar
    // Class path is too long for Windows ... So got to bundle it in a jar
    doFirst { classpath = files("$buildDir/classes/main", "$buildDir/resources/main", "$projectDir/gsp-classes", pathingJar.archivePath) }
    main = 'no.habitats.corpus.spark.SparkUtil'
    jvmArgs = '-Xmx60g -Xms12g -XX:+UseConcMarkSweepGC'.tokenize(" ")
    args = 'job=train'.tokenize(" ")
}
